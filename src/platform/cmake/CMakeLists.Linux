# Expects:
#  PLATFORM_ROOT
#
# Defines:
#  ARCAN_PLATFORM
#  FSRV_PLATFORM
#  LWA_PLATFORM
#  STDLIB
#
#  APPL_DEST
#  RES_DEST
#  APIMAN_DEST
#  MAN_DEST
#
# Modifies:
#  compile_options (global)
#  PLATFORM_HEADER (global)
#  OS_DYLIB_EXTENSION
#

set(PLATFORM_PATH ${PLATFORM_ROOT}/posix)

add_definitions(
	-D__UNIX
	-D__LINUX
	-DPOSIX_C_SOURCE
	-D_GNU_SOURCE
	-DPLATFORM_HEADER=\"${PLATFORM_ROOT}/platform.h\"
)

if (NOT APPL_DEST)
	set(APPL_DEST ${CMAKE_INSTALL_PREFIX}/share/arcan)
endif()

if (NOT SCRIPTS_DEST)
	set(SCRIPTS_DEST ${CMAKE_INSTALL_PREFIX}/share/arcan)
endif()

if (NOT RES_DEST)
	set(RES_DEST ${CMAKE_INSTALL_PREFIX}/share/arcan)
endif()

if (NOT APIMAN_DEST)
	set(APIMAN_DEST ${CMAKE_INSTALL_MANDIR}/man3)
endif()

if (NOT MAN_DEST)
	set(MAN_DEST ${CMAKE_INSTALL_MANDIR}/man1)
endif()

set(ARCAN_PLATFORM
	${PLATFORM_PATH}/mem.c
	${PLATFORM_PATH}/fmt_open.c
	${PLATFORM_PATH}/glob.c
	${PLATFORM_PATH}/map_resource.c
	${PLATFORM_PATH}/resource_io.c
	${PLATFORM_PATH}/strip_traverse.c
	${PLATFORM_PATH}/paths.c
	${PLATFORM_PATH}/dbpath.c
	${PLATFORM_PATH}/appl.c
	${PLATFORM_PATH}/sem.c
	${PLATFORM_PATH}/base64.c
	${PLATFORM_PATH}/time.c
	${PLATFORM_PATH}/warning.c
	${PLATFORM_PATH}/frameserver.c
	${PLATFORM_PATH}/fsrv_guard.c
	${PLATFORM_PATH}/fdpassing.c
	${PLATFORM_PATH}/namespace.c
	${PLATFORM_PATH}/launch.c
	${PLATFORM_PATH}/config.c
	${PLATFORM_PATH}/random.c
	${PLATFORM_PATH}/../stub/setproctitle.c
	${PLATFORM_PATH}/prodthrd.c
)

set_property(SOURCE ${PLATFORM_PATH}/fdpassing.c
	APPEND PROPERTY COMPILE_FLAGS -w)
set_property(SOURCE ${PLATFORM_PATH}/fdpassing.c
	APPEND PROPERTY COMPILE_DEFINITIONS NONBLOCK_RECV)
set_property(SOURCE engine/arcan_lua.c
	APPEND PROPERTY COMPILE_DEFINITIONS
		OS_DYLIB_EXTENSION=".so")

# not 'required' but enabled support for additional LED controllers,
# and in the near future, additional device support
pkg_check_modules(LIBUSB_1 libusb-1.0)
if(LIBUSB_1_FOUND)
	amsg("${CL_YEL}linux-usb\t${CL_GRN}detected${CL_RST}")
	set(ARCAN_NOLWA_DEFINITIONS ${ARCAN_DEFINITIONS} USB_SUPPORT)
	LIST(APPEND ARCAN_LIBRARIES ${LIBUSB_1_LINK_LIBRARIES})
	LIST(APPEND INCLUDE_DIRS ${LIBUSB_1_INCLUDE_DIRS})
	LIST(APPEND PLATFORM_SOURCES
		${EXTERNAL_SRC_DIR}/hidapi/hid.c
		${EXTERNAL_SRC_DIR}/hidapi/hidapi.h
	)
else()
	amsg("${CL_YEL}linux-usb\t${CL_RED}no libusb${CL_RST}")
endif()

set(LWA_PLATFORM ${ARCAN_PLATFORM})
list(APPEND LWA_PLATFORM ${PLATFORM_PATH}/open.c)

if (VIDEO_PLATFORM STREQUAL "egl-dri")
	list(APPEND ARCAN_PLATFORM ${PLATFORM_PATH}/psep_open.c)
else()
	list(APPEND ARCAN_PLATFORM ${PLATFORM_PATH}/open.c)
endif()
